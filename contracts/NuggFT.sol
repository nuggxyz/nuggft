// SPDX-License-Identifier: MIT

pragma solidity 0.8.4;

import './interfaces/IDotNuggFileResolver.sol';
import './interfaces/IDotNuggColorResolver.sol';

import './interfaces/IDotNugg.sol';
import './interfaces/INuggFT.sol';
import './NuggSwap.sol';
import './NuggETH.sol';

import './erc721/ERC721.sol';
import './erc2981/IERC2981.sol';

/**
 * @title Nugg Labs NFT Collection 0 - "NuggFT"
 * @author Nugg Labs - @danny7even & @dub6ix - 2021
 * @notice entrily onchain generative NFT and stakable auction contract
 * @dev this is art
 *
 * Note: epochs correlate directly to tokenIDs
 * Note: no images are stored in their final form - they are generated by view/pure functions at query time completly onchain
 * Note: the block hash corresponding to the start of an epoch is used as the "random" seed
 * Note: epochs are 256 blocks long as block hashes only exist for 256 blocks
 */
contract NuggFT is INuggFT, ERC721 {
    IDotNugg internal dotnugg;
    NuggETH internal nuggeth;
    NuggSwap internal nuggswap;
    IDotNuggFileResolver internal nuggin;

    uint256 public epochOffset;

    constructor(
        address _nuggeth,
        address _nuggswap,
        address _dotnugg,
        address _nuggin
    ) ERC721('Nugg Fungable Token', 'NuggFT') {
        dotnugg = IDotNugg(_dotnugg);
        nuggswap = NuggSwap(_nuggswap);
        nuggeth = NuggETH(_nuggeth);
        nuggin = IDotNuggFileResolver(_nuggin);

        require(nuggin.supportsInterface(type(IDotNuggFileResolver).interfaceId), 'NUG:LAUNCH:0');

        epochOffset = nuggswap.currentEpochId();
    }

    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view override(ERC721, IERC165) returns (bool) {
        return
            interfaceId == type(INuggMintable).interfaceId ||
            interfaceId == type(INuggSwapable).interfaceId ||
            super.supportsInterface(interfaceId);
    }

    function royaltyInfo(uint256, uint256 value) external view override returns (address, uint256) {
        return (address(nuggeth), (value * 1000) / 10000);
    }

    function nuggSwapMint(uint256 currentEpochId) external override returns (uint256 tokenId) {
        tokenId = currentEpochId - epochOffset;
        require(!_exists(tokenId), 'NFT:NSM:0');
        _safeMint(address(nuggswap), tokenId);
    }

    function _beforeTokenTransfer(
        address,
        address,
        uint256
    ) internal view override {
        require(msg_sender() == address(nuggswap), 'NFT:BTT:0');
    }

    /**
     * @inheritdoc ERC721
     */
    function tokenURI(uint256 tokenId) public view override returns (string memory res) {
        res = _generateTokenURI(tokenId, nuggswap.getSeedWithOffset(tokenId, epochOffset), address(nuggin));
    }

    function tokenURI(uint256 tokenId, address resolver) public view returns (string memory res) {
        res = _generateTokenURI(tokenId, nuggswap.getSeedWithOffset(tokenId, epochOffset), resolver);
    }

    /**
     * @notice calcualtes the token uri for a given epoch
     */
    function _generateTokenURI(
        uint256 tokenId,
        bytes32 seed,
        address resolver
    ) internal view returns (string memory) {
        string memory uriName = 'NuggFT {#}';
        string memory uriDesc = string(abi.encodePacked(seed));

        return dotnugg.nuggify(collection_, _getItems(seed), resolver, uriName, uriDesc, tokenId, seed, '');
    }

    // collection_
    bytes private collection_;

    // bases_
    bytes[] internal items_;

    /**
     * @notice gets unique attribtues based on given epoch and converts encoded bytes to object that can be merged
     */
    function _getItems(bytes32 seed) internal view returns (bytes[] memory res) {
        res = new bytes[](5);
        for (uint8 i = 0; i < res.length * 2; i++) {
            res[i] = items_[uint16(uint64(uint256(seed) >> (196 + i)) % items_.length)];
        }
    }
}
